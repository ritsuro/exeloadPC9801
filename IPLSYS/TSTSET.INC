; "TSTSET.INC" PC-9801UV11 make by R.H 1989/10/24
;
;	E X E L O A D    p r i n t   m a c r o .
;
;
;
; public subroutine.


;	ＰＲ	”表示文字列”
;	（改行無しプリント）

PR	MACRO	STRING
	LOCAL	L1,L2
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
ENDM


;	ＰＲＦ	”表示文字列”
;	（改行有りプリント）

PRF	MACRO	STRING
	LOCAL	L1,L2
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
	CALL	LFP
ENDM


;	ＩＮＰ
;	（文字列の入力）　出力　ＤＳ：ＳＩ＝入力文字列用
;		内部バッファの先頭アドレス（NULL=end of string）

INP	MACRO	STRING
	LOCAL	L1,L2,L3
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
	CALL	INPUT
	CALL	LFP
	CMP	BYTE PTR [SI],'Q'	;exit.
	JNZ	L3
	RET
L3:
ENDM


;	ＣＯＭＰ	”比較用文字列”
;	（ＤＳ：ＳＩとの比較）　入力　ＤＳ：ＳＩ＝比較対照文字列
;					（NULL=end of string）
;				出力　ｃｆ、ｚｆ

COMP	MACRO	STRING
	LOCAL	L1,L2
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	DI,OFFSET L1
	CALL	STRCMP
ENDM


;	ＬＦ
;	（改行）

LF	MACRO
	CALL	LFP
ENDM


;	ＰＲＶ	”表示文字列”，ＷＯＲＤ＿ＶＡＬ
;	（改行有りワード値プリント１０進）

PRV	MACRO	STRING,PARAM
	LOCAL	L1,L2
	PUSH	SI
	PUSH	DX
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
	MOV	DX,PARAM
	CALL	DECSTR
	CALL	PRINT
	POP	DX
	POP	SI
	CALL	LFP
ENDM


;	ＰＲＶＨ	”表示文字列”，ＷＯＲＤ＿ＶＡＬ
;	（改行有りワード値プリント１６進）

PRVH	MACRO	STRING,PARAM
	LOCAL	L1,L2
	PUSH	SI
	PUSH	DX
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
	MOV	DX,PARAM
	CALL	HEXSTR
	CALL	PRINT
	POP	DX
	POP	SI
	CALL	LFP
ENDM


;	ＰＲＶＢ	”表示文字列”，ＷＯＲＤ＿ＶＡＬ
;	（改行有りワード値プリント２進）

PRVB	MACRO	STRING,PARAM
	LOCAL	L1,L2
	PUSH	SI
	PUSH	DX
	PUSH	SI
	JMP	L2
DATA	SEGMENT	PUBLIC	'DATA'
	EVEN
L1	DB	STRING,0
	EVEN
DATA	ENDS
L2:	MOV	SI,OFFSET L1
	CALL	PRINT
	POP	SI
	MOV	DX,PARAM
	CALL	BINSTR
	CALL	PRINT
	POP	DX
	POP	SI
	CALL	LFP
ENDM


;	ＰＲＣＨ
;	（１キャラクタプリント）　入力　ＡＨ＝キャラクタコード

PRCH	MACRO
	LOCAL	L1
	PUSH	AX
	PUSH	BX
	PUSH	CX
	PUSH	DX
	CMP	AH,20H
	JNB	L1
	MOV	AH,'.'
L1:
	MOV	AL,AH
	CALL	CHPUT	;print of ascii character.
	POP	DX
	POP	CX
	POP	BX
	POP	AX
ENDM


;	ＳＥＴＳ	文字列名
;	（ＤＳ：ＳＩの文字列を文字列名のエリアへコピー）
;		入力　ＤＳ：ＳＩ＝セット文字列

SETS	MACRO	SNAME
	LOCAL	L1,L2
	PUSH	ES
	PUSH	SI
	MOV	AX,DS
	MOV	ES,AX
	CLD
	MOV	DI,OFFSET SNAME
L1:	LODSB
	CMP	AL,' '		; space.
	JB	L2
	STOSB
	JMP	L1
L2:	POP	SI
	POP	ES
ENDM

CODE	SEGMENT	PUBLIC	'CODE'

	ASSUME	CS:CODE,DS:DATA,ES:CODE,SS:CODE

	EXTRN	PRNAH:NEAR
	EXTRN	PRINT:NEAR
	EXTRN	INPUT:NEAR
	EXTRN	DECSTR:NEAR
	EXTRN	STRDEC:NEAR
	EXTRN	HEXSTR:NEAR
	EXTRN	STRHEX:NEAR
	EXTRN	BINSTR:NEAR
	EXTRN	STRBIN:NEAR
	EXTRN	STRCMP:NEAR
	EXTRN	CLS:NEAR
	EXTRN	CSLON:NEAR
	EXTRN	CSLOFF:NEAR
	EXTRN	INPCHR:NEAR
	EXTRN	CHPUT:NEAR
	EXTRN	KESCAP:NEAR
	EXTRN	KEYLOD:NEAR
	EXTRN	PRNAB:NEAR

LFP	PROC	NEAR
	PUSH	SI
	MOV	SI,OFFSET LFP1
	CALL	PRINT
	POP	SI
	RET
LFP	ENDP

DATA	SEGMENT	PUBLIC	'DATA'
LFP1	DB	0DH,0AH,0
	EVEN
DATA	ENDS

CODE	ENDS

;
;	end of "TSTSET.INC"
;
